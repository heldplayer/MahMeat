buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

sourceCompatibility = targetCompatibility = "1.8"
compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

ext.configFile = file "build.properties"
ext.configFile.withReader {
    // read config.  it shall from now on be referenced as simply config or as project.config
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

ext.minecraft_version = config.minecraft_version
ext.forge_version = config.forge_version

println "$project.name for Forge: " + minecraft_version + "-" + forge_version

version = minecraft_version + "-" + config.major_number + "." + config.minor_number + "." + config.patch_number + config.postfix
group = "blue.heldplayer.mahmeat"
archivesBaseName = "MahMeat"

minecraft {
    version = minecraft_version + "-" + forge_version
    runDir = "run"
    useDepAts = true

    mappings = "snapshot_20160518"
    makeObfSourceJar = false

    if (project.hasProperty("signature"))
        replace "@FINGERPRINT@", project.signature
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info', 'version.properties'

        // replace version and mcversion
        expand 'version': config.major_number + "." + config.minor_number + "." + config.patch_number + config.postfix,
                'mcversion': project.minecraft.version
    }

    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info', 'version.properties'
    }
}

// https://gist.github.com/matthewprenger/9b2da059b89433a01c1c
task signJar(type: SignJar, dependsOn: reobfJar) {
    onlyIf { project.hasProperty('keyStore') }

    if (!project.hasProperty('keyStore')) { // This shouldn't be possible, but onlyIf isn't working properly :/
        throw new StopExecutionException()
    }

    keyStore = project.keyStore // This needs to be a path to the keystore file
    alias = project.keyStoreAlias
    storePass = project.keyStorePass
    keyPass = project.keyStoreKeyPass
    inputFile = jar.archivePath
    outputFile = jar.archivePath
}

build.dependsOn signJar
